---
title: "Shaders"
excerpt_separator: "<!--more-->"
categories:
 - OpenGL 
tags:
 - opengl
---
{% include mathJax.html %}

Deep dive in to shaders

<!--more-->

**Table of Contents**
* TOC
{:toc}

## Shaders
As mentioned in the previous chapter, shaders are little programs that rest on the GPU. These programs are run for each specific section of the graphics pipeline. 

## GLSL
Shaders are written in the C-like language GLSL. GLSL is tailored for use with graphics and contains useful features specifically targeted at vector and matrix manipulation.

A shader typically has the following structure:

```cpp
#version version_number
in type in_variable_name;
in type in_variable_name;

out type out_variable_name;
  
uniform type uniform_name;
  
void main()
{
  // process input(s) and do some weird graphics stuff
  ...
  // output processed stuff to output variable
  out_variable_name = weird_stuff_we_processed;
}
```

When we're talking specifically about the *vertex shader* each input variable is also known as a **vertex attribute.** There is a maximum number of vertex attributes we're allowed to declare limited by the hardware. 

OpenGL guarantees there are always at least 16 4-component vertex attributes available, but some hardware may allow for more which you can retrieve by querying `GL_MAX_VERTEX_ATTRIBS`
{: .notice--warning}

```cpp
int nrAttributes;
glGetIntegerv(GL_MAX_VERTEX_ATTRIBS, &nrAttributes);
std::cout << "Maximum nr of vertex attributes supported: " << nrAttributes << std::endl;
```
## Types
GLSL has, like any other programming language, data types for specifying what kind of variable we want to work with. GLSL has most of the default basic types we know from languages like C: `int`, `float`, `double`, `uint` and `bool`.

GLSL also features two container types that we'll be using a lot, namely `vectors` and `matrices`.

### Vectors
A vector in GLSL is a 2,3 or 4 component container for any of the basic types just mentioned. They can take the following form (n represents the number of components):

* vecn: the default vector of n floats.
* bvecn: a vector of n booleans.
* ivecn: a vector of n integers.
* uvecn: a vector of n unsigned integers.
* dvecn: a vector of n double components.

Most of the time we will be using the basic vecn since floats are sufficient for most of our purposes.

The vector datatype allows for some interesting and flexible component selection called *swizzling*. Swizzling allows us to use syntax like this:

```cpp
vec2 someVec;
vec4 differentVec = someVec.xyxx;
vec3 anotherVec = differentVec.zyw;
vec4 otherVec = someVec.xxxx + anotherVec.yxzy;
```

We can also pass vectors as arguments to different vector constructor calls, reducing the number of arguments required:

```cpp
vec2 vect = vec2(0.5, 0.7);
vec4 result = vec4(vect, 0.0, 0.0);
vec4 otherResult = vec4(result.xyz, 1.0);
```

## Ins and outs
Shaders are nice little programs on their own, but they are part of a whole and for that reason we want to have inputs and outputs on the individual shaders so that we can move stuff around. 

GLSL defined the in and out keywords specifically for that purpose.

Each shader can specify inputs and outputs using those keywords and wherever an output variable **matches** with an input variable of the next shader stage they're passed along.
{: .notice--success}

But vertex and fragment shader differ a bit though, The vertex shader should receive some form of input otherwise it would be pretty ineffective. The vertex shader differs in its input, in that it receives its input straight from the vertex data.
(we use `layout (location = 0)` for this).
{: .notice--warning}

The other exception is that the fragment shader requires a `vec4` color output variable,since the fragment shaders needs to generate a final output color.
{: .notice--warning}

So if we want to send data from one shader to the other we'd have to declare an *output in the sending shader* and a **similar** *input in the receiving shader.* 

When the types and the names are equal on both sides OpenGL will link those variables together and then it is possible to send data between shaders (this is done when linking a program object `glLinkProgram`).

**Vertex shader**

```cpp
#version 330 core
layout (location = 0) in vec3 aPos; // the position variable has attribute position 0
  
out vec4 vertexColor; // specify a color output to the fragment shader

void main()
{
    gl_Position = vec4(aPos, 1.0); // see how we directly give a vec3 to vec4's constructor
    vertexColor = vec4(0.5, 0.0, 0.0, 1.0); // set the output variable to a dark-red color
}
```

**Fragment shader**

```cpp
#version 330 core
out vec4 FragColor;
  
in vec4 vertexColor; // the input variable from the vertex shader (same name and same type)  

void main()
{
    FragColor = vertexColor;
}
```

## Uniforms
Uniforms are another way to pass data from our application on the CPU to the shaders on the GPU. 

1. Uniforms are **global**. Global, meaning that a uniform variable is *unique* per shader program object, and can be accessed from any shader at any stage in the shader program. 
2. Whatever you set the uniform value to, uniforms will keep their values until they're either reset or updated.

To declare a uniform in GLSL we simply add the `uniform` keyword to a shader with a type and a name. 

```cpp
#version 330 core
out vec4 FragColor;
  
uniform vec4 ourColor; // we set this variable in the OpenGL code.

void main()
{
    FragColor = ourColor;
}   
```
We declared a uniform `vec4 ourColor` in the fragment shader and set the fragment's output color to the content of this uniform value. 

Since uniforms are global variables, we can define them in any shader stage we'd like! so no need to go through the vertex shader again to get something to the fragment shader. We're not using this uniform in the vertex shader so there's no need to define it there.

If you declare a uniform that isn't used anywhere in your GLSL code the compiler will silently remove the variable from the compiled version which is the cause for several frustrating errors; keep this in mind!
{: .notice--warning}

The uniform is currently empty; we haven't added any data to the uniform yet so let's try that.

We first need to find the index/location of the uniform attribute in our shader. Once we have the index/location of the uniform, we can update its values.

```cpp
float timeValue = glfwGetTime();
float greenValue = (sin(timeValue) / 2.0f) + 0.5f;
int vertexColorLocation = glGetUniformLocation(shaderProgram, "ourColor");
glUseProgram(shaderProgram);
glUniform4f(vertexColorLocation, 0.0f, greenValue, 0.0f, 1.0f);
```

Note that finding the uniform location does not require you to use the shader program first, but updating a uniform **does** require you to first use the program (by calling `glUseProgram`), because it sets the uniform on the currently active shader program.
{: .notice--warning}

### Uniforms- Postfixes
Because OpenGL is in its core a C library it does not have native support for function overloading, so wherever a function can be called with different types OpenGL defines new functions for each type required; `glUniform` is a perfect example of this. The function requires a specific postfix for the type of the uniform you want to set. A few of the possible postfixes are:

* f: the function expects a float as its value.
* i: the function expects an int as its value.
* ui: the function expects an unsigned int as its value.
* 3f: the function expects 3 floats as its value.
* fv: the function expects a float vector/array as its value.

Whenever you want to configure an option of OpenGL simply pick the overloaded function that corresponds with your type. In our case we want to set 4 floats of the uniform individually so we pass our data via `glUniform4f` (note that we also could've used the fv version).

## What happens if we add color vertex attribute?

```cpp
float vertices[] = {
    // positions         // colors
     0.5f, -0.5f, 0.0f,  1.0f, 0.0f, 0.0f,   // bottom right
    -0.5f, -0.5f, 0.0f,  0.0f, 1.0f, 0.0f,   // bottom left
     0.0f,  0.5f, 0.0f,  0.0f, 0.0f, 1.0f    // top 
};  

//vertex shader
#version 330 core
layout (location = 0) in vec3 aPos;   // the position variable has attribute position 0
layout (location = 1) in vec3 aColor; // the color variable has attribute position 1
  
out vec3 ourColor; // output a color to the fragment shader

void main()
{
    gl_Position = vec4(aPos, 1.0);
    ourColor = aColor; // set ourColor to the input color we got from the vertex data
}

//fragment shader
#version 330 core
out vec4 FragColor;  
in vec3 ourColor;
  
void main()
{
    FragColor = vec4(ourColor, 1.0);
}
```

Result will be:

![Alt Text]({{ site.baseurl }}/assets/opengl/gl7.png)

### Fragment Interpolation

**Why there is a huge color palette  even we only gave 3 colors?**

This is all the result of something called *fragment interpolation* in the fragment shader. When rendering a triangle the rasterization stage usually results in a lot more fragments than vertices originally specified.

The rasterizer then determines the positions of each of those fragments based on where they reside on the triangle shape.

Based on these positions, it interpolates all the fragment shader's input variables. Say for example we have a line where the upper point has a green color and the lower point a blue color. 

If the fragment shader is run at a fragment that resides around a position at *70%* of the line, its resulting color input attribute would then be a linear combination of green and blue; to be more precise: *30%* blue and *70%* green.

This is exactly what happened at the triangle. We have 3 vertices and thus 3 colors, and judging from the triangle's pixels it probably contains around 50000 fragments, where the fragment shader interpolated the colors among those pixels.
{: .notice--success}

OpenGL automatically interpolates out variables linearly across the surface of the triangle
{: .notice--success}

[Github Source](https://github.com/sakithanavod/OpenGLTests/tree/8b87be645ee9576d43d623a5208ad184233bdf16)